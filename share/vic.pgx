%grammar vic
%version 0.0.4
#COPYRIGHT: 2014 Vikas N Kumar <vikas@cpan.org>. All Rights Reserved

# uc-select is necessary.
program: uc-select header* statement* EOS

header: uc-config | comment
uc-select: /PIC <BLANK>+ (<uc-types>) <SEMI> <EOL>/

# P16F690X is fake just to show how to enumerate.
uc-types: /(?i:P16F690 | P16F690X)/
uc-config: /config/ + name config-expression SEMI EOL
config-expression: name <EQUAL> - (number-units | number) -

comment: /- <HASH> <ANY>* <EOL>/ | blank-line
blank-line: - EOL

ws: /<BLANK>/
_: /<ws>*/
__: /<ws>+/

statement: comment | instruction | expression | block

block: start-block statement* end-block
start-block: name - LCURLY - EOL?
end-block: - RCURLY - EOL?

instruction: name values* - SEMI EOL?

name: - identifier -
values: value-comma* value
value-comma: value COMMA
value: - (string | number-units | number | variable | block | validated-variable) -

expression: lhs-op-rhs | lhs-op | op-rhs
lhs-op: - variable - operator SEMI? EOL?
op-rhs: - operator - variable SEMI? EOL?
lhs-op-rhs: - variable - operator value SEMI EOL?

string: single-quoted-string | double-quoted-string

# most microcontrollers cannot do floating point math so ignore real numbers
number-units: number - units
# number handles both hex and non-hex values for ease of use
number: /(0x<HEX>+ | 0X<HEX>+ | <DIGIT>+)/
units: /(s | ms | us | kHz | Hz | MHz)/

validated-variable: identifier
variable: DOLLAR identifier
identifier: /(<ALPHA>[<WORDS>]*)/

operator: /((:
    <BANG> | <EQUAL> | <PERCENT> | <CARET> | <AMP> | <STAR> | <TILDE> | <DASH> |
    <PLUS> | <PIPE> | <SLASH> | <LANGLE> | <RANGLE>){1,2})/


single_quoted_string:
    /(:
        <SINGLE>
        ((:
            [^<BREAK><BACK><SINGLE>] |
            <BACK><SINGLE> |
            <BACK><BACK>
        )*?)
        <SINGLE>
    )/

double_quoted_string:
    /(:
        <DOUBLE>
        ((:
            [^<BREAK><BACK><DOUBLE>] |
            <BACK><DOUBLE> |
            <BACK><BACK> |
            <BACK><escape>
        )*?)
        <DOUBLE>
    )/

escape: / [0nt] /
